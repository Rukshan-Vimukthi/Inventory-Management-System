plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.25.0'
}

group 'com.example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url="https://jitpack.io"
    }
}

ext {
    junitVersion = '5.8.2'
}

sourceCompatibility = '17'
targetCompatibility = '17'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'com.example.inventorymanagementsystem'
    mainClass = 'com.example.inventorymanagementsystem.InventoryManagementApplication'
}

javafx {
    version = '17.0.2'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
    implementation('org.controlsfx:controlsfx:11.1.1')
    implementation('com.dlsc.formsfx:formsfx-core:11.5.0') {
        exclude(group: 'org.openjfx')
    }

    implementation('org.kordamp.bootstrapfx:bootstrapfx-core:0.4.0')
    implementation('org.kordamp.ikonli:ikonli-javafx:12.3.1')
    implementation('org.kordamp.ikonli:ikonli-fontawesome5-pack:12.3.1')
    implementation 'com.mysql:mysql-connector-j:8.3.0'

    implementation 'org.slf4j:slf4j-api:2.0.13'
    implementation 'ch.qos.logback:logback-classic:1.4.14'

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    imageName = 'SFC Inventory Management Application'
    launcher {
        name = 'SFC Inventory Management Application'
    }

    jpackage {
        installerType = 'exe' // Use 'dmg' or 'deb' as needed
        appVersion = '1.0.0'
        vendor = 'NXTDEV Co.'
        icon = 'src/main/resources/com/example/inventorymanagementsystem/images/sfc-logo.ico' // optional
        installerName = 'SFC Inventory Management Application Installer'

        installerOptions = [
                '--win-dir-chooser',
                '--win-menu',
                '--win-shortcut', // Optional for Windows only
                '--license-file', 'src/main/resources/LICENSE.rtf' // path to your license file
        ]
    }
}

jlinkZip {
    group = 'distribution'
}